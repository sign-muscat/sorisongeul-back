plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

ext {
    set('springAiVersion', "1.0.0-M1")
}

dependencies {

    /**
     * Spring boot 스타터 의존성
     */
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    /**
     * AI 의존성
     */
    implementation 'org.springframework.ai:spring-ai-openai-spring-boot-starter'

    /**
     * 유틸 리티 라이브 러리
     */
    implementation 'commons-io:commons-io:2.11.0'

    /**
     * QueryDSL 의존성
     */
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    /**
     * DB 커넥터
     */
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    /**
     * 보안 관련 의존성
     */
    implementation 'org.springframework.boot:spring-boot-starter-security' //TODO: 스프링시큐리티 의존성 - 셋팅 단계 에서만 제외
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    /**
     * Lombok 의존성
     */
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    /**
     * 개발 및 테스트 도구
     */
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
    }
}


/**
 * QueryDSL 설정
 */
def generated = 'src/main/generated'

tasks.withType(JavaCompile) {
    /* QClass 생성 위치 */
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

sourceSets {
    /* QClass 파일을 source 하위에 추가 */
    main.java.srcDirs += [generated]
}

clean {
    /* Gradle Clean 시 QClass 파일 삭제 */
    delete file(generated)
}

tasks.named('test') {
    useJUnitPlatform()
}
